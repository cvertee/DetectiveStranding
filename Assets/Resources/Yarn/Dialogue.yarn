title: detective_start
tags: 
colorID: 8
position: -182,-397
---
<<cb bg_street_night4>>
gr: Эта история началась с моего грандиозного провала...
<<cb bg_street_night3>>
gr: Я бы не назвал себя успешным детективом, хоть я и имел собственное детективное агенство, оно всегда было в упадке. 
gr: Последнее дело обернулось крахом, я не получил ни денег, ни репутации. 
<<cb bg_street_night2>>
gr: За аренду помещения нужно было платить, а без денег... 
gr: В общем и целом, мне пришло письмо с просьбой освободить помещение до начала следующего дня
<<cb bg_room>>
<<switchScene act1>>
gr: Сейчас час ночи и самое время, чтобы распрощаться с моим первым и последним офисом 
game: В игре присутствует интерактивный фон, кликайте на объекты, чтобы изучить их
===
title: act2_police
tags: 
colorID: 6
position: 1348,496
---
<<playAmbient Office>>
<<cb bg_hall>>

<<if $slept is true>>
    <<sc an hardangry center>>
   [[act2_slept]]
<<endif>>

<<if $slept is false>>
    <<sc an thinking left >>
    au: Грей подходит к секретарше, которая внимательно смотрит в окно.
    <<if $dream is true>>
        gr: *Она выглядит как девушка из моего сна... возможно не стоит её снова злить...*
        gr: *Почему я сказал "снова"?*
    <<endif>>
    gr: Кхм...
    gr: *Она стоит будто незамечая меня* 
    [[ Грубо поздороваться |badhello]]
    [[ Спокойно поздороваться |goodhello]]
<<endif>>
===
title: badhello
tags: 
colorID: 0
position: 1713,429
---
gr: Эй, здороваться вас тут не учили?
<<se an default>>
au: *Девушка отводит глаза от окна*
<<mc an center>>
<<se an lowangry>>
an: Кто ты?
gr: Детектив Грей.
<<se an thinking_2>>
gr: Уверен? Насколько я знаю ты помощник детектива Андерсона.
-> Самоуверенно и нагло
    gr: Это временно, скоро он будет моим помощником.
    <<se an thinking_unhappy>>
    an: Лампочку короной не задень, детектив.
-> Быть попроще
    <<se an default>>
    gr: Да, я помощник детектива Андерсона.
<<if $shower is false and $drink is true>>
    an: И, пожалуйста, детектив, в следующий раз принимайте душ, перед тем как приходить на работу.
    an: От Вас прёт как от... забудьте...
    gr: *Твою, я не успел помыться, от меня воняет алкоголем*
<<endif>>
[[aboutcase]]
===
title: goodhello
tags: 
colorID: 0
position: 1708,737
---
<<mc an center>>
gr: Здравствуйте, я новый детектив.
<<se an default>>
au: *Девушка отводит глаза от окна*
<<se an happy>>
an: Как зовут?
gr: Детектив Грей.
<<se an wonder_happy>>
an: Уверен? Насколько я знаю ты помощник детектива Андерсона.
-> Самоуверенно и нагло
    gr: Это временно, скоро он будет моим помощником.
    <<se an thinking_unhappy>>
    an: Лампочку короной не задень, детектив.
-> Быть проще
    gr: Да, я помощник детектива Андерсона.
    <<se an happy>>
    an: Я Мисс Фёдорова, секретарь мистера Андерсона.
<<if $shower is false and $drink is true>>
    an: И, пожалуйста, детектив, в следующий раз принимайте душ, перед тем как приходить на работу.
    an: От Вас прёт как от... забудьте...
    gr: *Твою, я не успел помыться, от меня воняет алкоголем*
<<endif>>
[[aboutcase]]
===
title: aboutcase
tags: 
colorID: 1
position: 2013,433
---
<<se an default>>
an: Ладно, Грей, у нас тут новое дело. Детектив Андерсон поручил тебе провестии допрос.
an: Вкратце тебе рассказать о нём или такой крутой, что сам во всём разберёшься?
[[ Расскажи |details]]
[[ Разберусь |nodetails]]
===
title: details
tags: 
colorID: 1
position: 2326,438
---
<<set $details to true>>
<<if $end is false>>
    an: У нас в отделении уборщица, она нашла труп в офисе на Оушн-Авенью.
    an:	Жертва мужчина, сотрудник офиса, был убит офисными ножницами в мужском туалете, камеры там не стоят, но...
    an:	...уборщица говорила, что видела как несколько человек заходило туда по-очереди с жертвой.
    an:	Допроси её, может чего узнаешь.
    <<if $slept is true>>   
        gr: *Этот репортаж я смотрел вчера по телевизору, может это мне поможет*
    <<endif>>
    <<set $end to true>>
<<endif>>
[[ Подробнее о жертве |diemandetails]]
[[ Подробнее о орудии убийства |weapondetails]]
[[ Подробнее о свидетельнице |witness]]
[[ Этого хватит |thatsall]]
===
title: nodetails
tags: 
colorID: 0
position: 2536,824
---
gr: Сам разберусь...
<<se an thinking_2_happy>>
an: Ну... как хотите... детектив.
au: Девушка саркастически выделила последнее слово.
[[act2_add]]
===
title: diemandetails
tags: 
colorID: 2
position: 2714,132
---
<<set $kill to true>>
gr: Кто жертва? 
<<se an thinking>>
an: Мужчина. 38 лет. Холост. Труп найден в 13.30 в мужском туалете. Работал в этом же офисе менеджером отвечал на звонки, 
an: Ходил на работу 5 дней в неделю. Жил в пригородке, до работы добирался долго. Семьи нет, материально обеспечен.
[[details]]
===
title: weapondetails
tags: 
colorID: 2
position: 2455,131
---
<<set $weapondetails to true>>
gr: У ножниц есть какие-то отличительные признаки? 
<<se an default>>
an: Обычные кухонные ножницы с красной ручкой. Таких миллион во всех офисах. 
gr: Можете послать кого-нибудь в их офис и принести ножницы, что есть на каждом столе?
an: Сделаем.
gr: Что с отпечатками? 
<<se an wonder>>
an: На них всё чисто, убийца позаботился о пальцах.
gr: Хмм... Где их нашли?
an: В трупе, удар был в шею, там они и оставались до приезда полиции.
[[details]]
===
title: thatsall
tags: 
colorID: 1
position: 2885,442
---
gr: Этого должно хватить.
[[act2_add]]
===
title: act2_add
tags: 
colorID: 1
position: 2897,827
---
[[ Поговорить ещё |act2_moretalk]]
[[ Провести допрос |act3_start]]
===
title: hat
tags: 
colorID: 0
position: 38,-674
---
<< set $hat to true >>
gr: Моя старая шляпа, её подарил мне мой наставник, когда я открывал этот офис
гр: Это было будто вчера, а ведь уже пять лет прошло
===
title: keys
tags: 
colorID: 0
position: -185,-673
---
<<set $key to true>>
gr: Ключи, меня попросили отдать их консьержу на входе
===
title: bottle
tags: 
colorID: 0
position: 37,-920
---
gr: Бутылка виски, почти закончилась. 
-> Допить.
    au: Мистер Грей допивает остатки алкоголя и выбрасывает бутылку в мусорное ведро.
    <<set $drink to true>>
-> Выкинуть.
    gr: Надо завязывать с выпивкой, ни к чему это не приведёт. 
    au: Мистер Грей берёт бутылку и выбрасывает в мусорное ведро.
===
title: safe
tags: 
colorID: 0
position: -183,-1147
---
-> Открыть сейф.
    gr: Хм, были бы здесь деньги я бы не собирал шмотки.
-> Отойти от сейфа.
    gr: Там пусто, в этом нет смысла.
===
title: window
tags: 
colorID: 0
position: 38,-1147
---
au: Посмотрев в окно Мистер Грей видит редко проезжающие машины и красивый ночной вид на город
===
title: plan
tags: 
colorID: 0
position: -185,-920
---
gr: Быть может если бы я не провалился, всё было бы подругому. 
au: Детектив рассматривает лица подозреваемых и всё ещё думает о том, где же он совершил ошибку.
===
title: act1_office
tags: 
colorID: 1
position: 196,-397
---
<<if $key is false and $hat is false>>
    gr: Мне нужно забрать шляпу и отдать ключи консьержу.
<<endif>>

<<if $key is true and $hat is false>>
    gr: Я забыл шляпу.
<<endif>>

<<if $key is false and $hat is true>>
    gr: Я забыл ключи.
<<endif>>

<<if $key is true and $hat is true>>
    gr: Больше меня здесь ничего не держит... 
    gr: Ну... время начать всё заново. 
    << switchScene act1_street >>
    [[act1_street]]
<<endif>>
===
title: act1_street
tags: 
colorID: 1
position: 453,-401
---
<<save>>
<<cb bg_street>>
gr: Вот так распалось моё агентство. 
gr: Теперь я был вынужден вернуться на работу в полицию.
gr: И начать... с должности помощника детектива.
гр: Для любого другого детектива это означало бы крах карьеры... 
гр: Да и я не сказал бы, что я этому рад, но...
ав: Детектив подходит к своему дому, он выглядит не бедно и не богато.
<<stopAllMusic MainTheme>>
<<cb bg_guestroom_detective_tvoff>>
<<playAmbient house>>
gr: Ладно, думаю стоит поужинать и ложиться спать. Надо не забыть поставить будильник на завтра.
game: Вы можете осмотреть дом, чтобы перемещаться по квартире нажимайте на левую и правую стороны экрана.
<<switchScene home>>
<<set $on to true>>
<<set $on1 to true>>
<<set $slept to true>>
===
title: act1_house
tags: 
colorID: 1
position: 1064,-423
---
<<switchScene null>>
gr: Завтра тяжёлый день. 
gr: В восемь утра мне уже надо быть в департаменте.
-> Лечь спать
    <<cb bg_bedroom_detective_tvoff>>
    gr: Да, я лучше высплюсь.
    <<switchScene act2>>
    [[act2_start]]
-> Продолжить осмотр
    [[guestroom]]
===
title: act2_start
tags: 
colorID: 8
position: 1336,-421
---
<<stopAllMusic LastPath>>
<<stopAllMusic MainTheme>>

<<if $slept is true and $dream is false>>
   [[slepttrue_dreamfalse]]
<<endif>>

<<if $slept is true and $dream is true>>
    [[dream]]
<<endif>>

<<if $slept is false and $dream is false>>
    [[sleptfalse_dreamfalse]]
<<endif>>
===
title: act2_slept
tags: 
colorID: 0
position: 1889,124
---
an: Ты где пропадал чёрт тебя подери!?
an: Девушка чуть ли не кричит на Детектива Грея.
    -> Притвориться умным
        gr: Извините, Мисс, Миссис...
        an: Просто Мисс Фёдорова, детектив!
        gr: Извините Мисс Фёдорова, я заблудился на дороге под названием жизнь...
        an: Чего?
        gr: ...
        <<kc an>>
        [[end1]]
    -> Сказать правду.
        gr: Извините, Мисс, Миссис...
        an: Просто Мисс Фёдорова, детектив!
        gr: Извините, Мисс Фёдорова, проспал! Вчера был тяжёлый...
        an: Не хочу слушать твои отговорки, будешь работать дополнительно.
        gr: *Чёрт, надо же было...А ведь первое впечатление самое важное...*
        an: Чего задумался детектив, за работу быстро!
        gr: Есть!
        <<if $shower is false and $drink is true>>
            an: И, пожалуйста, детектив, в следующий раз принимайте душ, перед тем как приходить на работу.
            an: От Вас прёт как от... забудьте...
            gr: *Твою, я не успел помыться, от меня воняет алкоголем*
        <<endif>>
        [[aboutcase]]
    -> Застрял в пробке.
        gr: Извините, Мисс, Миссис...
        an: Просто Мисс Фёдорова, детектив!
        gr: Извините, Мисс Фёдорова, попал в пробку! Эти улицы таки...
        an: Ты меня за дуру держишь?
        gr: Так точно, Мисс Фёдорова!
        an: Что ты сказал? 
        <<kc an>>
        [[end1]]
    -> Убежать.
        gr: Извините, Мисс, Миссис...
        an: Просто Мисс Фёдорова, детектив!
        gr: Извините, Мисс Фёдорова, пока!
        an: Детектив Грей разворачивается и направляется к выходу...
        an: Эй, ты сумасшедший что ли? 
        <<kc an>>
        [[end1]]
===
title: end1
tags: 
colorID: 0
position: 1868,-425
---
<<cb bg_sleep>>
<<stopAllAmbient Office>>
<<playMusic LastPath>>
<<set $dream to true>>
gr: Вот так и закончилась моя история...
gr: Я так и не нашёл работу... спустя 3 дня меня попросили выехать из квартиры
gr: А через 4 дня я умер с голоду...
[[act2_start]]
===
title: sleptfalse_dreamfalse
tags: 
colorID: 0
position: 1113,-14
---
<<cb bg_kitchen>>
<<playAmbient kitchen>>
au: Грей проснулся за час до начала работы, что он успеет сделать?
-> Позавтракать
    au: ВЫ плотно завтракаете так, что хватит на весь день
    <<set $breakfast to true>>
-> Принять душ
    au: Вы смываете с себя запах.
    <<set $shower to true>>
<<cb bg_street_day>>
<<stopAllAmbient kitchen>>
<<playAmbient street>>
gr: В городе как всегда спешка. 
<<cb bg_policedeportment>>
gr: А вот и место куда я буду ходить всю оставшуюся жизнь...
<<stopAllAmbient street>>
[[save1]]
===
title: dream
tags: 
colorID: 0
position: 1369,-21
---
<<cb bg_bedroom_detective>>
<<stopAllAmbient Office>>
<<stopAllMusic LastPath>>
<<playAmbient house>>
gr: А!
au: Грей вскакивает с кровати.
gr: Что за сон...
gr: Чёрт, я опаздываю!
<<stopAllAmbient house>>
<<cb bg_street_day>>
<<playAmbient street>>
au: Начинающий детектив Грей бежит сломя голову на работу, он опаздывает чуть больше чем на час.
<<cb bg_policedeportment>>
gr: Какой же я критин. 
gr: Девять часов, опоздал всего на час...
gr: Блин, всё как во сне...
<<stopAllAmbient street>>
[[save1]]
===
title: slepttrue_dreamfalse
tags: 
colorID: 0
position: 1629,-15
---
<<playAmbient street>>
<<cb bg_street_day>>
gr: Чёрт я опаздываю!
<<cb bg_street_day2>>
au: Начинающий детектив Грей бежит сломя голову на работу, он опаздывает чуть больше чем на час.
<<cb bg_street_day3>>
gr: Чёрт, ну и остолоп же...
<<cb bg_policedeportment>>
gr: Так время девять, опоздал всего на час, Грей, ничего страшного...Люди там понимающие...
<<stopAllAmbient street>>
[[save1]]
===
title: act2_moretalk
tags: 
colorID: 0
position: 3221,821
---
[[ Кто такой Детектив Андерсон? |act2_anderson]]
[[ Кем ты работаешь? |act2_fedorova]]
[[ Какая у меня зарплата? |act2_wages]]
[[ Во сколько заканчивается рабочий день? |act2_endday]]
[[ Закончить разговор |act3_start]]
===
title: act3_start
tags: 
colorID: 1
position: 2896,1638
---
<<kc an>>
<<stopAllAmbient office>>
<<cb bg_quest>>
gr: Свидетельницы ещё нет, стоит подготовиться.
-> Проверить камеру
    [[act3_cam]]
-> Вспомнить детали дела
    [[act3_details]]
-> Закончить подготовку
    [[act3_questioning]]
===
title: act2_anderson
tags: 
colorID: 0
position: 3438,483
---
gr: Расскажи больше о Детективе Андерсоне
<<se an wonder_happy>>
an: Что именно тебя интересует?
gr: Он хорош?
an: Хм...
<<se an thinking_2>>
au: *Девушка задумчиво посмотрела в сторону окна*
an: Он...
<<se an default>>
an: Андерсон явно лучший среди нас, не так давно он раскрыл крайне сложное дело. 
an: Какой-то маньяк ловил детей и запирал их в сточных канавах, а на месте пропажи оставлял оригами.
gr: Как его поймали? 
an: Этот дурак возле каждого места преступления оставлял подсказки, где находится та или иная жертва. 
an: А один раз вообще сам припёрся и напал на журналистку, всё было бы хорошо, если бы не его телесложение.
gr: Телосложение? 
an: Чувак был два метра ростом и весом в 100 килограмм, такого не потеряешь...
gr: В чём тогда заслуга Андерсона?
an: Хоть маньяка быстро поймали, детей найти всё ещё было сложно. 
an: Он единственный, кто смог сузить круг поиска и нашёл потерянных детей.
gr: *Чёрт, он действительно хорош*
[[act2_moretalk]]
===
title: act2_fedorova
tags: 
colorID: 0
position: 3640,737
---
<<se an thinking>>
gr: Чем ты занимаешься?
an: Смотрю в окно...
gr: Нет...я, в смысле, по работе...
<<se an default>>
an: Я помощница детектива и секретарь, могу выехать на дело, могу отвечать на звонки, разбираться с документами и так далее.
gr: Не много задач для одного человека? 
an: Мне в самый раз, а вот тебе стоило бы начать работу.
-> Ты будешь выезжать со мной на дела?
    gr: Раз я и ты помощники детектива Андерсона, ты будешь выезжать со мной на дела? 
    <<se an happy>>
    an: Закатай губу, Грей, ты пока ещё не в штате.
    gr: Что? Нет, ты не о том поду... 
    au: Мисс Фёдорова Вас перебивает
    an: Да, мы будем ездить вместе на те дела, которые Андерсон нам отдаст.
    [[act2_moretalk]]
-> Задать другой вопрос.
    [[act2_moretalk]]
===
title: act2_wages
tags: 
colorID: 0
position: 3638,1000
---
gr: Какая у меня будет зарплата?
au: *Мисс Фёдорова улыбнулась*
<<se an happy>>
an: А какую ты хочешь? 
gr: Ну...
-> 1000$ в месяц
    gr: Тясячи в месяц мне бы хватало.
    an: Ну, думаю сойдёмся на 750$ в месяц, ты всё-таки новенький.
    an: Как покажешь себя в лучшем свете, так сразу повысят.
-> 10000$ в месяц
    gr: Десяти тысяч в месяц мне бы хватало.
    an: Ну, думаю сойдёмся на 750$ в месяц.
    gr: 750$? 
    an: Угу, а что ты хотел. 
    an: В России средня зарплата вообще 250$, так что радуйся.
-> 100000$ в месяц
    gr: Ста тысяч в месяц мне бы хватало.
    <<se an wonder_happy>>
    an: Ну, думаю сойдёмся на 750$ в месяц.
    gr: 750$? 
    an: Угу, а что ты хотел. 
    an: В России средня зарплата вообще 250$, так что радуйся.
[[act2_moretalk]]
===
title: act2_endday
tags: 
colorID: 0
position: 3381,1218
---
gr: Во сколько заканчивается моя смена?
an: Как и у всех, в пять вечера, но...
gr: Но? 
<<se an happy>>
an: Но хорошие детективы ведь всегда остаются после работы, да, Детектив Грей?
gr: Кхм... *она хочет, чтобы я работал сверхурочно*
<<if $slept is true>>
    an: Тем более ты сегодня опоздал, так что твоя смена продлена на 2 часа.
    gr: *Чёрт, не стоило смотреть телевизор перед сном*
<<endif>>
[[act2_moretalk]]
===
title: act3_details
tags: 
colorID: 0
position: 2891,1961
---
[[ Орудие убийства |act3_weapon]]
[[ Жертва |act3_kill]]
[[ Свидетельница |act3_witness]]
[[ Хватит |act3_start]]
===
title: act3_questioning
tags: 
colorID: 1
position: 3206,1644
---
gr: Время начинать...
<<playMusic Time>>
au: В камеру заходит полицейский и свидетельница.
mi: Почему я вообще должна отвечать на вопросы? Я всё рассказала! Меня дома дети ждут!
po: Сядьте пожалуйста, детектив задаст Вам несколько вопросов и Вы будете свободны.
mi: Хорошо, но не больше, чем несколько.
<<if $weapondetails is true>>
    po: Детектив, как Вы просили. 
    au: Полицейский передаёт Вам пакет в котором лежат ножницы из офиса.
    gr: Спасибо.
<<endif>>
<<sc mi angry center>>
au: Полийеский уходит и закрывает камеру, а женщина садится на стул и смотрит прямо на Вас.
-> Поздароваться первым
    gr: Здравствуйте, я Детектив Грей, как Вас зовут?
    <<se mi normal>>
    mi: Добрый день, Детектив Грей, я Мисс Грачёва.
-> Спросить имя
    gr: Как вас зовут?
    <<se mi angry>>
    mi: Миссис Грачёва, а что здороваться Вас не учили?
    -> Поздароваться
        gr: Здравствуйте...
        mi: И Вам здрасте!
    -> Промолчать
        gr: ...
[[act3_chapter1]]
===
title: act3_weapon
tags: 
colorID: 0
position: 2540,2278
---
<<if $weapondetails is true>>
    gr: Жертву убили с помощью офисных ножниц. 
    gr: Я попросил принести мне любые ножницы из их офиса, чтобы сравнить их с орудием убийства.
    gr: Также убийца позаботился об отпечатках, на ножницах всё чисто.
<<endif>>
<<if $details is true and $weapondetails is false>>
    gr: Жертву убили с помощью офисных ножниц. 
    gr: Это всё что мне известно, возможно стоило подробнее расспросить об этом.
<<endif>>
<<if $details is false and $weapondetails is false>>
    gr: Я не знаю чем была убита жертва...
    gr: Возможно стоило подробнее расспросить об этом.
<<endif>>
[[act3_details]]

//gr: У ножниц есть какие-то отличительные признаки? 
//an: Обычные кухонные ножницы с красной ручкой. Таких миллион во всех офисах. 
//gr: Можете послать кого-нибудь в их офис и принести ножницы, что есть на каждом столе?
//an: Сделаем.
//gr: Что с отпечатками? 
//an: На них всё чисто, убийца позаботился о пальцах.
//gr: Хмм... Где их нашли?
//an: В трупе, удар был в шею, там они и оставались до приезда полиции.

//an: У нас в отделении уборщица, она нашла труп в офисе на Оушн-Авенью.
//an:	Жертва была убита офисными ножницами в мужском туалете, камеры там не стоят, но...
//an:	...уборщица говорила, что видела как несколько человек заходило туда по-очереди с жертвой.
//an:	Допроси её, может чего узнаешь.
===
title: act3_kill
tags: 
colorID: 0
position: 2893,2272
---
<<if $kill is true>>
    gr: Жертва - мужчина, 38 лет. Холост. Труп найден в 13.30 в мужском туалете. 
    gr: Работал в этом же офисе менеджером отвечал на звонки. 
    gr: Ходил на работу 5 дней в неделю. Жил в пригородке, до работы добирался долго. Семьи нет, материально обеспечен.
<<endif>>
<<if $details is true and $kill is false>>
    gr: О жертве мне известно, что это мужчина и работал он в этом же офисе.
    gr: Убит был в мужском туалете... Судя по всему это всё...
<<endif>>
<<if $details is false and $kill is false>>
    gr: У меня нет информации о жертве...
    gr: Нужно было спросить у Мисс Фёдоровой.
<<endif>>
[[act3_details]]

//gr: Кто жертва? 
//an: Мужчина. 38 лет. Холост. Труп найден в 13.30 в мужском туалете. Работал в этом же офисе менеджером отвечал на звонки, 
//an: Ходил на работу 5 дней в неделю. Жил в пригородке, до работы добирался долго. Семьи нет, материально обеспечен.

//an: У нас в отделении уборщица, она нашла труп в офисе на Оушн-Авенью.
//an: Жертва мужчина, сотрудник офиса, был убит офисными ножницами в мужском туалете, камеры там не стоят, но...
//an: ...уборщица говорила, что видела как несколько человек заходило туда по-очереди с жертвой.
//an:	Допроси её, может чего узнаешь.
===
title: act3_witness
tags: 
colorID: 0
position: 3251,2271
---
<<if $witness is true>>
    gr: Свидетель - Жещина, 56 лет, детей нет, всю жизнь проработала преподавателем. По образованию Физик-ядерщик. 
    gr: Сейчас работает уборщицей, якобы нашла уже лежачий труп. 
    gr: Она говорила, что видела, как незадолго до обнаружения убийства, вместе с жертвой в туалет заходило несколько человек.
    gr: Два года назад её уволили за то, что она насильно заставляла студентов ВУЗа проходить её платные курсы.
<<endif>>
<<if $details is true and $witness is false>>
    gr: Свидетель - уборщица, которая якобы нашла уже лежачий труп. 
    gr: Она говорила, что видела, как незадолго до обнаружения убийства, вместе с жертвой в туалет заходило несколько человек.
<<endif>>
<<if $details is false and $witness is false>>
    gr: Я ничего не знаю о свидетельнице...
<<endif>>
[[act3_details]]

//gr: Кто жертва? 
//an: Мужчина. 38 лет. Холост. Труп найден в 13.30 в мужском туалете. Работал в этом же офисе менеджером отвечал на звонки, 
//an: Ходил на работу 5 дней в неделю. Жил в пригородке, до работы добирался долго. Семьи нет, материально обеспечен.

//an: У нас в отделении уборщица, она нашла труп в офисе на Оушн-Авенью.
//an: Жертва мужчина, сотрудник офиса, был убит офисными ножницами в мужском туалете, камеры там не стоят, но...
//an: ...уборщица говорила, что видела как несколько человек заходило туда по-очереди с жертвой.
//an:	Допроси её, может чего узнаешь.

//gr: Что изветсно о свидетельнице?
//an: Жещина, 56 лет, всю жизнь проработала преподавателем. По образованию Физик-ядерщик. 
//an: Два года назад её уволили за то, что она насильно заставляла студентов ВУЗа проходить её платные курсы.
//gr: Это было что-то вроде взятки? 
//an: Вроде того, она не ставила зачёты, пока студент не пройдёт у неё платный курс и запугивала экзаменами, классика в общем.
//gr: Были случаи агрессии? 
//an: Кхм... Около пяти лет назад она сильно унизила студента за то, что он не принёс конспект на пару, парень чуть не сбросился с крыши.
//gr: *Чёрт, надо быть добрее к своим ученикам*
===
title: witness
tags: 
colorID: 2
position: 2200,131
---
<<set $witness to true>>
gr: Что изветсно о свидетельнице?
<<se an thinking_2>>
an: Жещина, 56 лет, ни детей ни мужа, всю жизнь проработала преподавателем. По образованию Физик-ядерщик. 
an: Два года назад её уволили за то, что она насильно заставляла студентов ВУЗа проходить её платные курсы.
gr: Это было что-то вроде взятки? 
an: Вроде того, она не ставила зачёты, пока студент не пройдёт у неё платный курс и запугивала экзаменами, классика в общем.
[[details]]
===
title: act3_cam
tags: 
colorID: 0
position: 2520,1642
---
gr: Камера... работает...
<<set $cam to true>>
[[act3_start]]
===
title: act3_chapter1
tags: 
colorID: 1
position: 3565,1645
---
gr: Расскажите мне подробно о том, как Вы обнаружили жертву.
<<se mi idontknow>>
mi: Я, как обычно, после обеда начала уборку в офисе, ведь я в отличии от некоторых выполняю свою работу ответственно.
mi: Сначала убрала прихожую, сам офис, а потом уже зашла в уборные, сначала в женскую, там я слышала, как из-за стены идут непонятные звуки.
mi: Ну думаю это туалет, мало ли у кого-какие проблемы, убралась в женской уборной, вышла из неё, зашла в мужскую и увидела труп.
gr: Вы говорили, что видели, как вместе с жертвой в уборную заходило ещё несколько человек.
mi: Да, именно, когда я шла убирать женский туалет, я видела, как несколько мужчин прошли в мужской.
-> Во сколько Вы услышали звуки из-за стены?
    gr: В какое примерное время Вы услышали странные звуки за стеной?
    mi: Ну где-то в 13.30
    -> Надавить
        gr: Судя по моим данным в 13.30 Вы обнаружили труп
        <<se mi lying>>
        gr: Ой ну минутой позже, минутой раньше, я старенькая уже, память подводит.
		gr: *Хм, я ничего не добился*
    -> Оставить
        gr: Это ничего не значит...
-> Как долго Вы там убирались?
    gr: Вы долго убирали в женской уборной?
    mi: Минут пять, я довольно быстро всё делаю. 
    gr: Вы видели снова тех людей, что заходили вместе с жертвой уборную?
    mi: Нет, когда я дошла до мужской, там уже никого не было. Они наверное ушли, пока я убиралась.
    gr: Эти люди, сотрудники офиса? 
    <<se mi lying>>
    mi: Не знаю, у меня плохая память на лица, даже Вас, Детектив, не запомню.
    gr: *Хм, я ничего не добился*
[[act3_chapter2]]
===
title: act3_chapter2
tags: 
colorID: 1
position: 3839,1643
---
[[ Задать вопросы |act3_questions]]
[[ Вынести вердикт |act3_verdict]]
===
title: act3_questions
tags: 
colorID: 0
position: 4649,1644
---
<<if $var4 is false>>
    [[ Вы знали жертву? |knownkillman]]
<<endif>>
<<if $weapondetails is true and $var2 is false>>
    [[ Спросить работает ли она в перчатках |gloves]]
<<endif>>
<<if $weapondetails is true and $var1 is false>>
    [[ Сравнить орудие убийства и ножницы с офиса |comparison]]
<<endif>>
[[ Назад |act3_chapter2]]
===
title: act3_Blame
tags: 
colorID: 0
position: 4231,2195
---
<<if $proof1 is true and $proof2 is true>>
    gr: *Хм, пока всё указывает на то, что Мисс Грачёва была причастна к убийству*
    gr: *Она носит перчатки, когда убирается и вполне могла не оставить следы*
    gr: *Также она призналась, что ножницы были её...*
    gr: *Но это всё ещё не доказывает то, что она убийца*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: *А сейчас лучше оставить Мисс Грачёву в отделении до выяснения обстоятельств*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения оставить Вас в отделении полиции до тех пор,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is false and $proof2 is true>>
    gr: *Я не уверен, но возможно Мисс Грачёва что-то скрывает*
    gr: *Она носит перчатки, когда убирается и вполне могла не оставить следы*
    gr: *Но это всё ещё не доказывает то, что она убийца*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: *А сейчас лучше оставить Мисс Грачёву в отделении до выяснения обстоятельств*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения оставить Вас в отделении полиции до тех пор,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is true and $proof2 is false>>
    gr: *Я не уверен, но возможно Мисс Грачёва что-то скрывает*
    gr: *Она призналась, что ножницы были её...*
    gr: *Но это всё ещё не доказывает то, что она убийца*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: *А сейчас лучше оставить Мисс Грачёву в отделении до выяснения обстоятельств*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения оставить Вас в отделении полиции до тех пор,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is false and $proof2 is false>>
    gr: Мне не в чем её обвинить, придётся её отпустить...
    [[act3_badend]]
<<endif>>
===
title: act3_verdict
tags: 
colorID: 1
position: 3839,2513
---
[[ Оставить её в отделении до выяснения обстоятельств |act3_Blame]]
[[ Отпустить её |act3_letgo]]
[[ Назад |act3_chapter2]]
===
title: gloves
tags: 
colorID: 2
position: 4220,1400
---
gr: Вы работаете в перчатках Миссис Грачёва? 
mi: Конечно, я же чистоплотная Дама. 
gr: *Это может объяснять отсутствие отпечатков на орудии убийства, стоит ли мне на неё надавить?*
-> Надавить.
    gr: Миссис Грачёва, на орудии убийства отсутствуют отпечатки убийцы, Вы можете как-то это объяснить? 
    <<se mi angry>>
	mi: Непоняла, щенок, ты что МЕНЯ ОБВИНЯЕШЬ? 
	gr: Я хочу узнать почему нет отпечатков.
	mi: Убийца просто помыл ножницы перед тем как их бросить
    -> Надавить
        gr: Орудие убийства было в трупе, убийца не мог их помыть, для этого ему нужно было сначала их вынуть из трупа.
        gr: Следовательно убийца был в перчатках.
        mi: Ты чёртов, ДЕТЕКТИВИШКА! ДА ТЫ ЗНАЕШЬ КТО Я!?
        <<set $proof2 to true>>
        -> Обвинить
            gr: Мисс Грачёва, Вы были единственным человеком в перчатках в уборной!
            mi: Что!? Да ты кем себя возомнил? 
            <<se mi evil>>
            mi: Хахахахахахахах...
            au: *Старуха судорожно смеётся*
            gr: *Почему она смеётся?*
            mi: А с чего ты взял, что убийца просто не мог надеть перчатки перед убийством?
            gr: ...
            gr: Чёрт, обвинять её было лишним.
            mi: Слабый Вы детектив, Грей.
        -> Сохранять спокойствие
            gr: Я всё ещё неуверен в том, что убийца не мог надеть перчатки перед нападением.
            gr: Возможно это просто совпадение... или нет...
    -> Оставить 
        gr: Чёрт, звучит логично. 
-> Оставить как есть.
    gr: *Не хочу на неё давить*
<<set $var2 to true>>
[[act3_chapter2]]
===
title: comparison
tags: 
colorID: 2
position: 4222,1095
---
gr: *Нужно сравнить ножницы*
au: Вы открываете пакет, который передал Вам полицейский и обнаруживаете там несколько одинаковых ножниц.
gr: *Кхм... они отличаются от тех, что были найдены в трупе*
gr: *Их ведь закупают оптом*
gr: Миссис Грачёва, я попросил принести мне несколько ножниц из офиса, где было совершено убийство.
gr: Мне их принесли и я сравнил их с орудием убийства. Они разные.
mi: И что? Не у всех же одинаковые ножницы.
-> Надавить 
    gr: Судя по всему компания закупает ножницы оптом, у всех сотрудников на столах лежат одинаковые ножницы.
    gr: А эти... они отличаются. Как думаете, что это значит?  
    <<se mi fuckname>>
    mi: Мне то откуда знать? НЕ Я ЖЕ ДЕТЕКТИВ!
    gr: Я могу Вам объяснить.
    gr: Это значит, что убийство было совершено не сотрудником офиса, а кем-то другим.
    -> Надавить
        gr: Вы говорите, что в уборную вместе с жертвой зашли сотрудники офиса, которых вы не разглядели, так?
        <<se mi lying>>
        mi: Что!? Я так не говорила!
        gr: Вы так говорили, Мисс Грачёва, камеры всё пишут. 
        mi: Хорошо, возможно, Я ЗАБЫЛА! У меня плохая память на лица...
        gr: Единcтвенный человек, который был в одной комнате с жертвой, не считая сотрудников... Это Вы, Миссис Грачёва.
        gr: Отвечайте! Это Ваши ножницы!?
        <<se mi angry>>
        mi: О боже... Как ВАС там, ДЕТЕКТИВ СЕРЫЙ, ДА ЭТО МОИ НОЖНИЦЫ, НО ЭТО НИКАК МЕНЯ НЕ ОБВИНЯЕТ! 
        mi: чТО ВООБЩЕ ЗА ЧУШЬ! Я ПОЗВОНИЛА В ПОЛИЦИЮ, ЗАЧЕМ МНЕ СЕБЯ СДАВАТЬ!7 
        gr: Миссис Грачёва, как Ваши ножницы попали в руки убийцы? 
        mi: ДА Я ЗНАЮ ЧТОЛЕ? УКРАЛ У МЕНЯ, ПОКА Я ПОЛЫ МЫЛА В СОТЫЙ РАЗ! 
        gr: *Отлично, я узнал, что ножницы пренадлежат Грачёвой*
        <<set $proof1 to true>>
    -> Оставить как есть
    gr: Вы точно не видели никого, кроме сотрудников офиса?
    mi: Ну... я не уверена...
    mi: Может там был кто-то, пока я не видела!
-> Оставить как есть
    gr: Она права...
<<set $var1 to true>>
[[act3_chapter2]]
===
title: knownkillman
tags: 
colorID: 0
position: 4231,1873
---
gr: Вы знали жертву?
<<se mi angry>>
mi: Да, знала.
-> Почему тогда ВЫ ЕГО УБИЛИ!?
    gr: За что, тогда, Мисс Грачёва, ВЫ УБИЛИ ЭТОГО БЕДНОГО ПАРНЯ!?
    mi: ЧТО!?
    gr: Я всё знаю! ЭТО ВЫ! 
    mi: Вы сумашедший!
    gr: Сознавайся, корга!
    <<se mi lying>>
    mi: ЧТО ЗА НЕУВАЖЕНИЕ! Я ЕГО НЕ УБИВАЛА! 
    gr: *кхм... не знаю на что я рассчитывал, но у неё была странная реакция на это*
-> Расскажите о нём
    gr: Расскажите о жертве подробнее.
    <<se mi normal>>
    mi: Я с ним встречалась каждое утро...
    mi: Он постоянно опаздывал, когда я заканчивала мыть полы, он всегда проходил грязными ногами по нему
    <<se mi angry>>
    mi: И так каждое утро, мне приходилось мыть полы дважды...
    mi: Больше я о нём ничего не знаю.
<<set $var4 to true>>
[[act3_chapter2]]
===
title: act3_end
tags: 
colorID: 6
position: 4641,2517
---
<<stopAllMusic Time>>
<<kc mi>>
<<cb bg_hall>>
<<playAmbient Office>>
<<sc an default center>>
gr: Ну...
an: Только не говори, что ты не смог её допросить...
gr: ...
<<se an hardangry>>
an: И ЧТО ТЫ МОЛЧИШЬ!?
gr: Ладно, ладно, у меня есть несколько зацепок. 
<<se an happy>>
an: Что ты узнал?
gr: Мне кажется с Мисс Грачёвой что-то не так.
<<if $proof1 is true and $proof2 is true>>
    gr: Отсутствие отпечатков пальцев на орудии убийства объясняется тем, что на убийце были перчатки.
    << se an thinking_2>>
    an: А Грачёва работает в перчатках?
    gr: Именно.
    << se an thinking_2_unhappy>>
    an: Убийца мог просто надеть их перед убийством, это ничего не доказывает.
    gr: Зато она призналасть, что ноженицы были её...
    <<se an default>>
    an: Как её ножницы попали в руки убийцы?
    gr: Возможно они не попадали, что если Грачёва убийца?
    an: Хм... это всё равно ничего не доказывает, но это... по меньшей мере странно...
    an: Нам нужно больше данных, нужно съездить на место преступления и расспросить сотрудников.
<<endif>>
<<if $proof1 is false and $proof2 is true>>
    gr: Отсутствие отпечатков пальцев на орудии убийства объясняется тем, что на убийце были перчатки.
    << se an thinking_2>>
    an: А Грачёва работает в перчатках?
    gr: Именно.
    an: Убийца мог просто надеть их перед убийством...
    an: Хм... это всё равно ничего не доказывает, но это... по меньшей мере странно...
    an: Нам нужно больше данных, нужно съездить на место преступления и расспросить сотрудников.
<<endif>>
<<if $proof1 is true and $proof2 is false>>
    gr: Она призналасть, что ноженицы были её...
    << se an thinking_2>>
    an: Как её ножницы попали в руки убийцы?
    << se an thinking_2_unhappy>>
    gr: Возможно они не попадали, что если Грачёва убийца?
    << se an happy>>
    an: Хм... это всё равно ничего не доказывает, но это... по меньшей мере странно...
    an: Нам нужно больше данных, нужно съездить на место преступления и расспросить сотрудников.
<<endif>>
au: СПАСИБО, ЧТО ПОИГРАЛИ В ДЕМО-ВЕРСИЮ НАШЕЙ ИГРЫ!
===
title: act3_badend
tags: 
colorID: 0
position: 4218,2517
---
gr: Мисс Грачёва, Вы можете идти...
<<se mi evil>>
mi: Слава богу! Наконец это кончилось...
au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
<<stopAllMusic Time>>
<<kc mi>>
<<cb bg_hall>>
<<playAmbient Office>>
<<sc an default center>>
gr: Ну...
an: Только не говори, что ты не смог...
gr: ...
an: И ЧТО ТЫ МОЛЧИШЬ!?
gr: Ты же сказала не говорить.
an: !!!
<<kc an>>
<<cb bg_sleep>>
<<stopAllAmbient Office>>
<<playMusic LastPath>>
gr: Возможно я зря выбрал карьеру детектива...
gr: Хороший детектив должен цепляться за каждую деталь, а я пропускал диалоги и боялся давить на подозреваемых
gr: Так и закончилась моя история...
<<load>>
===
title: act3_letgo
tags: 
colorID: 0
position: 4217,2865
---
<<if $proof1 is true and $proof2 is true>>
    gr: *Хм, пока всё указывает на то, что Мисс Грачёва была причастна к убийству*
    gr: *Она носит перчатки, когда убирается и вполне могла не оставить следы*
    gr: *Также она призналась, что ножницы были её...*
    gr: *Но это всё ещё не доказывает то, что она убийца, придётся её отпустить*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения отпустить Вас,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is true and $proof2 is false>>
    gr: *Я не уверен, но возможно Мисс Грачёва что-то скрывает*
    gr: *Она призналась, что ножницы были её...*
    gr: *Но это всё ещё не доказывает то, что она убийца, придётся её отпустить*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения отпустить Вас,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is false and $proof2 is true>>
    gr: *Я не уверен, но возможно Мисс Грачёва что-то скрывает*
    gr: *Она носит перчатки, когда убирается и вполне могла не оставить следы*
    gr: *Но это всё ещё не доказывает то, что она убийца, придётся её отпустить*
    gr: *Возможно мне стоит поговорить с Мисс Фёдровой и разузнать больше*
    gr: Мисс Грачёва, спасибо за разговор, я принял решения отпустить Вас,
    gr: пока мы не найдём ещё подозреваемых.
    <<se mi whyme>>
    mi: "Ещё подозреваемых"!? Я что теперь подозреваемая!?
    gr: В ваших показаниях найдены ошибки, я бы не хотел делать поспешных выводов.
    au: *Вы встаёте из-за стола и просите полицейского открыть камеру*
    [[act3_end]]
<<endif>>

<<if $proof1 is false and $proof2 is false>>
    gr: Мне не в чем её обвинить, придётся её отпустить...
    [[act3_badend]]
<<endif>>
===
title: patrul_start
tags: 
colorID: 8
position: -477,428
---
<<playAmbient Office>>
<<cb bg_office2>>
gr: 
<<cb bg_policedeportment2>>
gr: 
<<cb bg_street>> 
gr: 
<<cb bg_coffee>>
gr:
<<cb bg_guestroom_patrul>> 
gr: 
<<cb bg_bedroom_patrul>> 
gr: 
<<cb bg_guestroom_patrul>> 
gr: 
<<cb bg_street_day>>
gr: 
<<cb bg_coffee>>
gr:
<<cb bg_policedeportment>>
gr: 
<<cb bg_policedeportment2>>
gr:
===
title: buppy_start
tags: 
colorID: 8
position: -299,1224
---
<<cb bg_street_day2>>
gr: 
<<cb bg_street_day3>>
gr: 
<<cb bg_guestroom_buppy>> 
gr: 
<<cb bg_bedroom_buppy>>
gr: 
<<cb bg_guestroom_buppy>> 
gr: 
<<cb bg_street_day>> 
gr:
===
title: Menu
tags: 
colorID: 0
position: -2237,427
---
gr: dadaad
[[ начать игру |characters]]
===
title: characters
tags: 
colorID: 0
position: -1928,433
---
<<cb characters>>
<<switchScene CharacterScreen>>
===
title: try_detective
tags: 
colorID: 1
position: -1614,114
---
<<cb try_detective1>>
g: 
<<cb try_detective2>>
g: 
<<cb try_detective3>>
g:
<<set $detective to true>>
[[pre-history]]
===
title: patrul
tags: 
colorID: 1
position: -1617,433
---
<<cb patrul1>>
g:
<<cb patrul2>>
g: 
<<cb patrul3>>
g:
<<set $patrul to true>>
[[pre-history]]
===
title: buppy
tags: 
colorID: 1
position: -1621,723
---
<<cb buppy1>>
g: 
<<cb buppy2>>
g: 
<<cb buppy3>>
g: 
<<cb buppy4>>
g:
<<set $buppy to true>>
[[pre-history]]
===
title: pre-history
tags: 
colorID: 2
position: -1310,435
---
<<cb quest0>>
g:
<<cb quest1>>
g: 
<<cb quest2>>
g: 
<<cb quest>>
-> Невидимость
-> Телекинез
-> Перемещение во времени
-> Контроль разума
-> Телепортация
<<cb quest3>>
g: 
<<cb quest>>
-> Да, меня не устраивает моя профессия
-> Нет, меня всё устраивает
-> Я не работаю
<<cb quest4>>
g: 
<<cb quest>>
-> Я бы что-нибудь украл
-> Совершил бы особо тяжкое
-> Я бы порвал карточку
<<cb quest5>>
g: 
<<cb quest>>
-> Да
    <<set $goodsleep to true>>
-> Нет
-> Это единственное, что я делаю хорошо
    <<set $goodsleep to true>>
<<cb quest6>>
g: 
<<cb quest>>
-> Президентом 
-> Известной личностью
-> Богачём
-> Талантливой личностью
-> Никем, остался бы собой
<<cb quest7>>
g: 
<<cb quest>>
-> Да, я бы мог большего
    <<set $badwork to true>>
-> Нет, я выкладываюсь максимально
-> Мне хватает
<<cb quest8>>
g: 
<<cb quest>>
-> Так кто я? Зачем этот тест? 
    <<cb quest8.1>>
    g:
    <<cb quest>>
    -> Хорошо, давайте продолжим
-> Хорошо, давайте продолжим
<<cb year>>
g: 
<<cb city>>
g:
[[characterss]]
===
title: characterss
tags: 
colorID: 0
position: -873,430
---
<<if $patrul is true>>
    [[patrul_start]]
<<endif>>

<<if $detective is true>>
    [[detective_start]]
<<endif>>

<<if $buppy is true>>
    [[buppy_start]]
<<endif>>
===
title: kitchen
tags: 
colorID: 0
position: 1349,-796
---
<<cb bg_kitchen>>
<<playAmbient kitchen>>
<<switchScene kitchen>>
===
title: guestroom
tags: 
colorID: 0
position: 710,-849
---
<<playAmbient house>>
<<switchScene home>>

<<if $on is true>>
    <<cb bg_guestroom_detective_tvoff>>
<<else>>
    <<cb bg_guestroom_detective_night>>
<<endif>>
===
title: tv
tags: 
colorID: 0
position: 584,-1203
---
<<if $tv is false>>
    <<if $night is false>>
        <<switchScene null>>
        au: Мистер Грей сел на диван и включил телевизор.
        <<playSound butt>>
        <<cb bg_guestroom_detective>>
        <<playMusic tv>>
        ав: На одном из каналов шли вечерние новости. Ведущая теленовостей начала репортаж:
        ne: Сегодня в офисе на Оушн-Авенью произошло зверское убийство, нападавший нанёс жертве точечный удар острым предметом в шею.
        ne: Полиция говорит, что преступник будет обнаружен в кратчайшие сроки, так как одна из уборщиц офиса видела подозреваемых.
        но: Так же сегодня в выпуске: MacroSoft прекратила поддержку своей операционной системы Windous 99 и Windous MR
        но: Чемпионом мира по футболу в Германии стала сборная Италии, победив сборную Франции в послематчевых пенальти.
        но: Научный мир пришёл к соглашению снять с Плутона звание девятой планеты солнечной системы, теперь это карликовая планета.
        но: Все эти репортажи после прогноза погоды...
        <<playSound butt>>
        <<stopAllMusic tv>>
        <<cb bg_guestroom_detective_tvoff>>
        <<switchScene home>>
        <<set $tv to true>>
    <<endif>>
<<else>>
<<if $night is false>>
        <<switchScene null>>
        au: Мистер Грей сел на диван и включил телевизор. На экране появился прогноз погоды. Ведущая начала репортаж:
        <<playSound butt>>
        <<cb bg_guestroom_detective>>
        <<playMusic tv>>
        ne: В городе опять ожидается солнечная погода. Лето выдаётся жарким, правительство советует носить головные уборы.
        но: Наш спонсор магазин "Шляпник" предлагает Вам приобрести шляпу, будь то для холодной, то для жаркой погоды.
        но: Сегодня акция, позвонившие в течение двух часов получат скидку в 20% на первую покупку...
        ав: Грей выключает телевизор не желая больше слушать рекламу.
        <<playSound butt>>
        <<stopAllMusic tv>>
        <<cb bg_guestroom_detective_tvoff>>
        <<switchScene home>>
    <<endif>>
<<endif>>
===
title: window2
tags: 
colorID: 0
position: 807,-1201
---
<<if $night is false>>
    <<switchScene null>>
    -> Посмотреть в окно
        ав: Детектив разводит пальцами жалюзи и смотрит сквозь образовавшуюся щель на улицу.
        гр: У меня довольно спокойный район, мне повезло арендовать этот дом.
    -> Отойти
    <<switchScene home>>
<<endif>>
===
title: sofa
tags: 
colorID: 0
position: 584,-1428
---
<<if $night is false>>
    <<switchScene null>>
    -> Сесть на диван
        ав: Детектив Грей садится на диван и непроизвольно начинает думать о своей карьере
        гр: Чёрт, снова возвращаться в отделение. Я был так близок к успеху.
        гр: Так долго стараться для того, чтобы открыть свою частную фирму...
        гр: И так бездарно всё провалить...
        <<if $badwork is true>>
            гр: Мне нужно было больше работать...
            ав: Грей уже пять минут сидит и думает, что он много ленился и мало работал.
        <<else>>
            гр: Ведь я делал всё по максимуму, работал не покладая рук.
            ав: Грей уже пять минут сидит и думает о несправедливости жизни
        <<endif>>
        гр: Грей ты та ещё бездарность...
        ав: Детектив Грей ещё минут 15 сидит на диване и мысленно ругает сам себя
        гр: Ладно, так не пойдёт, соберись, Грей, завтра тяжёлый день.
    -> Продолжить осмотр
    <<switchScene home>>
<<endif>>
===
title: plate
tags: 
colorID: 0
position: 1237,-1033
---
<<switchScene null>>
ав: Требуется женщина
<<switchScene kitchen>>
===
title: icecamera
tags: 
colorID: 0
position: 1467,-1034
---
<<switchScene null>>
ав: Грей подходит к холодильнику и открывает его. 
ав: Пялясь около минуты в него он понимает, что еда у него практически кончилась.
-> Пойти в кафе
    [[cafe]]
-> Не ужинать
<<switchScene kitchen>>
===
title: bedroom
tags: 
colorID: 0
position: 1874,-830
---
<<switchScene bedroom>>
<<if $on1 is true>>
    <<cb bg_bedroom_detective>>
<<else>>
    <<cb bg_bedroom_detective_night>>
<<endif>>
===
title: bed
tags: 
colorID: 0
position: 1873,-1053
---
<<if $night1 is false>>
    <<switchScene null>>
    -> Лечь спать
        <<switchScene null>>
        gr: Лучше выспаться, завтра рано вставать
        <<cb bg_sleep>>
        ав: Детектив Грей закрывает глаза и пытается уснуть. 
        <<if $goodsleep is false>>
            ав: У него явно плохой сон, несколько часов он просто ворочался пытаясь найти удобную позу для сна.
            ав: Уснул Детектив буквально за пару часов до звонка будильника
        <<else>>    
            ав: У детектива хороший сон. Он легко уснул и будет отлично себя чувствовать, когда проснётся.
        <<endif>>
        [[act2_start]]
    -> Продолжить осмотр
        [[bedroom]]
<<endif>>
===
title: alarm
tags: 
colorID: 0
position: 2097,-1050
---
<<switchScene null>>
<<if $night1 is false>>
    -> Поставить будильник на 8 утра
        <<set $slept to false>>
        гр: Ну теперь я хоть не опоздаю
    -> Оставить всё как есть
<<endif>>
<<switchScene bedroom>>
===
title: off
tags: 
colorID: 0
position: 1874,-1283
---
<<if $on1 is true>>
    <<set $on1 to false>>
    <<cb bg_bedroom_detective_night>>
    <<playSound butt>>
    <<set $night1 to true>>
<<else>>
    <<playSound butt>>
    <<cb bg_bedroom_detective>>
    <<set $on1 to true>>
    <<set $night1 to false>>
<<endif>>
===
title: guestroom_off
tags: 
colorID: 0
position: 806,-1428
---
<<if $on is true>>
    <<set $on to false>>
    <<cb bg_guestroom_detective_night>>
    <<playSound butt>>
    <<set $night to true>>
<<else>>
    <<playSound butt>>
    <<cb bg_guestroom_detective_tvoff>>
    <<set $on to true>>
    <<set $night to false>>
<<endif>>
===
title: cafe
tags: 
colorID: 0
position: 1460,-1288
---
<<playAmbient cafe>>
<<cb bg_street_night3>> 
ав: Детектив пошёл в сторону своего любимого кафе. 
ав: Кафе работает до трёх ночи поэтому он всегда успевал зайти туда после работы
<<cb bg_coffee_evening>>
гр: Мне всегда нравилось это место, здесь спокойно.
ав: Грей проходит мимо человека играющего на гитаре, под ним расположен чехол от гитары, в который люди кидают деньги.
-> Кинуть монетку
    ав: Детектив кидает несколько монет музыканту.
-> Пройти мимо
    ав: Детектив проходит мимо.
<<cb bg_coffee_evening2>>
ав: Грей садится за свободный столик и зовёт официантку, которая подходит к нему в ту же секунду.
ав: Детектив не смог рассмотреть лица официантки, то ли из-за усталости, то ли из-за освещения.
оф: Что будете заказывать?
ав: Голос официантки был черезвычайно милым из-за чего Детектив расстерялся.
гр: Кхм... дайте меню...
оф: Ой, извините, забыла. 
ав: Официантка протягивает Детективу меню блюд и он начинает медленно изучать каждое блюдо.
[[cafe_part2]]
===
title: cafe_part2
tags: 
colorID: 0
position: 1228,-1283
---
-> Фасолевый суп
    гр: Пожалуйста фасолевый суп
-> Пышная пицца
    гр: Пожалуйста пышную пиццу
-> Панкейки
    гр: Пожалуйста панкейки
-> Запеканка
    гр: Запеканку пожалуйста
-> Фаршированный перец
    гр: Фаршированный перец пожалуйста
оф: Хорошо, принесу заказ через 5 минут.
ав: Спустя 5 минут официантка возвращается и расставляет на столе заказ.
-> Спросить номер телефона
    гр: Кхм...
    ав: Грей неуверенно прокашлялся.
    оф: Вам плохо? 
    гр: Нет, нет... я...
    гр: Дадите мне свой номер телефона?
    ав: Казалось будто официантка слегка улыбнулась.
    оф: Извините, я не знакомлюсь с клиентами.
    ав: Официантка не дав ничего ответить отворачивается и уходит.
    гр: Да, глупая была идея.
    ав: Детектив берёт счёт, который оставила официантка и находит там бумажку.
    ав: Перевернув её он обнаруживает там номер телефона написанный ручкой.
    гр: Ладно, может идея была не такая уж и глупая.
    <<set $girl to true>>
-> Не делать этого
    ав: Не понимая почему, Грей упускает свой шанс познакомиться с девушкой.
<<cb bg_coffee_evening>>
ав: Спустя 15 минут Детектив съедает всё, оставляет на столе деньги и уходит домой...
[[guestroom]]
===
title: save1
tags: 
colorID: 0
position: 1342,248
---
<<save>>
[[act2_police]]
===
